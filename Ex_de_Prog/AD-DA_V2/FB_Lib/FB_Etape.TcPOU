<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.6">
  <POU Name="FB_Etape" Id="{50c1e382-4cf1-4a5f-afba-95bff47d6bed}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Etape
VAR_INPUT
	tTimeQ :TIME;			//Time to stay in one state after which xTimeQ will go to TRUE
END_VAR
VAR_OUTPUT
	tTimeState: TIME ; 	//passed time in the actual state
	xTimeQ : BOOL ;		//is TRUE when time passed in the actual State is greather then tTimeQ
	xTransition:BOOL; 	//information that the state has just changed. For use when entering a new State the first time
	iNo:INT:=0; 		//State number running at the moment
	sMess :STRING ; 	//Information message about the current running state
END_VAR
VAR
	iMemNo:INT;				//Used for xTransition. Saves the actual state of the machine
	fbTon:ton;				//Timer for the actual State
	fbRtrig:R_trig;			//detector for rising edges
	xMemModeStepByStep:BOOL;	//Mem for the actual running Mode (see method mActualisation)
	
END_VAR

]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="mActualisation" Id="{55744620-5161-4abd-b432-fd0783d3fbeb}">
      <Declaration><![CDATA[//executed at each state-machine-loop
METHOD PUBLIC mActualisation
VAR_INPUT
	xModeStepByStep: BOOL; //activate the Stepping-Mode (TRUE=ON)
	xSetNextStep: BOOL; //go to the next Step on rising eddge
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//manage StepByStep
//-----------------

//Detect rising edge
fbRtrig(CLK:=xSetNextStep);
//copy the mode input to use it in piSVal
xMemModeStepByStep:=xModeStepByStep;

//detect a state changement in the state machine
xTransition:=iNo<>iMemNo;

//start Method when the state has changed
IF xTransition THEN
	mTransition();
END_IF

//manage the Timer and associated Variabels
//-----------------------------------------
fbTON(IN:=(iNo=iMemNo), PT:=T#71582M47S295MS, ET=>tTimeState); //will be resetted when the state is changed

//going to TRUE when longer in the actual State than tTimeQ
xTimeQ:=(tTimeState>=tTimeQ);

//save the last running state
iMemNo:=iNo;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="mTransition" Id="{960a3bb8-e288-4cc0-8bf2-106dbf4cf42a}">
      <Declaration><![CDATA[//Executed when a State has changed
METHOD PRIVATE mTransition
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//make a new String to display
sMess:=CONCAT('iNo:=',int_to_string(iNo));]]></ST>
      </Implementation>
    </Method>
    <Property Name="piSVal" Id="{44c0fe94-6db4-475f-9cae-a9d0b624ff50}">
      <Declaration><![CDATA[//going to the next step while paying attention to StepByStep Mode
PROPERTY PUBLIC piSVal : INT]]></Declaration>
      <Set Name="Set" Id="{7ec0f9d5-2735-47c0-874b-16c79be50589}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[IF NOT xMemModeStepByStep THEN
	//Step by step mode is off
	iNo:=piSVal;
	ELSIF fbRtrig.Q THEN
		//a button press on "xSetNextStep" has been detected
		iNo:=piSVal;
END_IF]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="piVal" Id="{f5f1b136-586e-4c97-aac0-06478fd38b0a}">
      <Declaration><![CDATA[//going to the next step without paying attention to StepByStep Mode
PROPERTY PUBLIC piVal : INT]]></Declaration>
      <Set Name="Set" Id="{dde331f7-ecc0-4893-85d7-30f5c71cd963}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[//set the next step
iNo:=piVal;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="psMess" Id="{df60808c-3cda-4140-ab5d-8edfa9b946e5}">
      <Declaration><![CDATA[//
PROPERTY PUBLIC psMess : STRING]]></Declaration>
      <Set Name="Set" Id="{9c407712-a6f6-43bf-9b6d-deaa9428735e}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[//write the description of the actual step to sMess
IF xTransition THEN
	sMess:=psMess;
END_IF]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>