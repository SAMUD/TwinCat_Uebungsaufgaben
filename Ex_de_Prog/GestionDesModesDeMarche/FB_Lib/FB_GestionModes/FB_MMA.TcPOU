<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.6">
  <POU Name="FB_MMA" Id="{0e7ce976-5b63-45cb-af38-a3b8fef59482}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_MMA
VAR_INPUT
END_VAR
VAR_OUTPUT
	iEtape:INT;
END_VAR
VAR
	fbEtape:FB_Etape;
	p_InitVar : p_InitVar;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[fbEtape.mActualisation(FALSE,FALSE);

//on active l'arret d'urgence si l'entree devient false
IF NOT fbBtn.xAru AND fbEtape.iNo>10 AND fbEtape.iNo<800 THEN
	fbEtape.piVal:=900; 
END_IF

//on fait un reset
IF fbBtn.xReset AND fbEtape.iNo>10 AND fbEtape.iNo<800 THEN
	fbEtape.piVal:=800;
END_IF


CASE fbEtape.iNo OF
	0://mise en marche du système
		fbEtape.piVal:=10;
	
	10://OB100 Siemens
		p_InitVar(); //initialiser les vars (propre a la machine)
		fbEtape.piVal:=50;
	
	50:fbEtape.psMess:='Selection du mode MMA';
		IF (stParam.stStatus.eMMA=Auto) THEN
			fbEtape.piVal:=300;
		ELSIF (stParam.stStatus.eMMA=Man) THEN
			fbEtape.piVal:=210;
		ELSIF (stParam.stStatus.eMMA=Init) THEN
			fbEtape.piVal:=100;
		END_IF
		
	//--------------------------------------------------------------
	//Init
	//--------------------------------------------------------------
	100:fbEtape.psMess:='Cycle Init | Attente de fbBtn.xStart';
		IF (fbBtn.xStart) THEN
			fbEtape.piVal:=110;
		ELSIF (stParam.stStatus.eMMA<>Init) THEN
			fbEtape.piVal:=50;
		END_IF
	110:fbEtape.psMess:='Cycle Init | Attente fin';
		IF fbCycleInit.iEtape=999 THEN
		fbEtape.piVal:=50;
		END_IF
	111:fbEtape.psMess:='Cycle Init | Finir';
		IF fbCycleInit.xBusy THEN
		fbEtape.piVal:=50;
		END_IF
		
	//--------------------------------------------------------------
	//Manual
	//--------------------------------------------------------------
	210:fbEtape.psMess:='Cycle Manuel | Attente de changement de mode';
		IF (fbCycleMan.iEtape=999) THEN
		fbEtape.piVal:=211;
		END_IF
	211:fbEtape.psMess:='Cycle Manuel | Finir';
		IF (fbCycleMan.xBusy) THEN
		fbEtape.piVal:=50;
		END_IF
		
	//--------------------------------------------------------------
	//Auto
	//--------------------------------------------------------------
	300:fbEtape.psMess:='Mode Auto | Attente de fbBtn.xStart';
		IF (fbBtn.xStart) THEN
			fbEtape.piVal:=340;
		ELSIF (stParam.stStatus.eMMA<>Auto) THEN
			fbEtape.piVal:=50;
		END_IF
	
	340:fbEtape.psMess:='Mode Auto | Cycle Init - attente fin';
		//si le cycle INIT est terminé alors on continue
		fbEtape.piVal:=341;
		
	341:fbEtape.psMess:='Mode Auto | Cycle Init - Finir';
		//si le cycle INIT est terminé alors on continue
		IF fbCycleInit.iEtape=999 THEN
			fbEtape.piVal:=350;
		END_IF
		
	350:fbEtape.psMess:='Mode Auto | Production - attend signal Stop';
		IF (fbBtn.xStop OR stParam.stStatus.xErreur ) THEN 
			fbEtape.piVal:=390;
		END_IF
		
	390:fbEtape.psMess:='Mode Auto | Attendre fin';
		IF (NOT fbCycleAuto.xBusy) THEN
			fbEtape.piVal:=50;
		END_IF
		
	//--------------------------------------------------------------
	//Reset
	//--------------------------------------------------------------
	800:fbEtape.psMess:='Reset | Procédure de reset, stop imédiat';
		fbEtape.piVal:=810;
	810:fbEtape.psMess:='Reset | Ordre pour pilotage reste sur cycle';
		fbEtape.piVal:=899;
	899:fbEtape.psMess:='Reset | Fin de procédure Reset';
		fbEtape.piVal:=50;
		
	//--------------------------------------------------------------
	//ARU
	//--------------------------------------------------------------
	900:fbEtape.psMess:='ARU | Procédure de ARU';
		fbEtape.piVal:=910;
	910:fbEtape.psMess:='ARU | Ordre pour pilotage reste sur cycle';
		fbEtape.piVal:=950;
	950:fbEtape.psMess:='ARU | ok - Attente de quittance';
		IF fbBtn.xAru THEN
			fbEtape.piVal:=999;
	END_IF
	999:fbEtape.psMess:='ARU | Fin de procédure ARU';
		fbEtape.piVal:=50;
END_CASE


iEtape:=fbEtape.iNo;]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>